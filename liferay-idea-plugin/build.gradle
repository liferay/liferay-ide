buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
	}

	repositories {
		maven {
			url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
		}
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
}

plugins {
	id "org.jetbrains.intellij" version "0.3.1"
	id "de.undercouch.download" version "3.4.2"
}

apply plugin: 'com.liferay.source.formatter'
apply plugin: 'java'

check.dependsOn checkSourceFormatting

checkSourceFormatting {
	maxLineLength = 120
}

dependencies {
	compile group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.4.0"
	compile group: "org.apache.ant", name: "ant", version: "1.10.1"
	compile group: "org.codehaus.jackson", name: "jackson-core-asl", version: "1.9.13"
	compile group: "org.codehaus.jackson", name: "jackson-mapper-asl", version: "1.9.13"
}

formatSource {
	maxLineLength = 120
}

intellij {
	version "2018.1.2"
	plugins 'gradle', 'maven'
}

patchPluginXml {
	sinceBuild '172'
	untilBuild ''
}

repositories {
	maven {
		url "https://cdn.lfrs.sl/repository.liferay.com/nexus/content/groups/public"
	}
}

task downloadBlade(type: Download) {
	def pomFile = new File("../build/parent/pom.xml")

	def project = new XmlSlurper().parse(pomFile)

	def bladeLatestUrl = new URL(project.properties.'blade-latest-download-url'.toString())

	src bladeLatestUrl
	dest 'src/main/resources/libs/blade.jar'
	onlyIfModified true
}

task verifyBlade(type: Verify, dependsOn: downloadBlade) {
	def pomFile = new File("../build/parent/pom.xml")

	def project = new XmlSlurper().parse(pomFile)

	def bladeLatestMd5 = project.properties.'blade-latest-md5'.toString()

	src 'src/main/resources/libs/blade.jar'
	algorithm 'MD5'
	checksum bladeLatestMd5
}

compileJava {
	dependsOn verifyBlade
}

version = '0.1.1'